apply plugin: 'maven-publish'

apply from: "../version.gradle"
apply from: "../credentials.gradle"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "${libraryGroup}"
            artifactId = "${libraryArtifactId}"
            version = "${libraryVersion}"

            afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
            artifact sourcesJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (!"unspecified".equals(dependency.name)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "${snapshotRepoUrl}"

            credentials {
                username = "${snapshotGradleUsername}"
                password = "${snapshotGradlePassword}"
            }
        }
    }
}